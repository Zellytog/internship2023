open import Agda.Primitive
open import Cubical.Core.Everything
open import Cubical.Data.Empty renaming (rec to zrec)
open import Cubical.Data.Nat
open import Cubical.Data.Sigma
open import Cubical.Data.Unit
open import Cubical.Foundations.Everything
open import Cubical.Foundations.Pointed.Homogeneous
open import Cubical.Functions.FunExtEquiv
open import Cubical.HITs.PropositionalTruncation renaming (rec to proprec)
open import Cubical.HITs.SetTruncation renaming (elim to setelim)
open import Cubical.HITs.Truncation
open import Cubical.Homotopy.Connected
open import Cubical.Homotopy.HSpace
open import Cubical.Structures.Pointed

module Sign.AbGrp where

private
  variable
    â„“ : Level

infixr 30 _â†’â‚_ _â‰ƒâ‚_

-- Alias for the properties of being a group, i.e. being an homogeneous connected pointed groupoid

connectedPath : Type â„“ â†’ Type â„“
connectedPath X = (x : X) (y : X) â†’ âˆ¥ x â‰¡ y âˆ¥â‚

homoâˆ™Ab : âˆ€ {â„“} â†’ (X : Type â„“) â†’ X â†’ Type (lsuc â„“)
homoâˆ™Ab {â„“} X x = Î£[ e âˆˆ ((y : X) â†’ PathP (Î» i â†’ Î£[ X âˆˆ Type â„“ ] X) (X , x) (X , y))] (e x â‰¡ refl)

AbGrp : âˆ€ â„“ â†’ Type (lsuc â„“)
AbGrp â„“ = Î£[ X âˆˆ Type â„“ ] (Î£[ x âˆˆ X ] ((connectedPath X) Ã— (isGroupoid X) Ã— (homoâˆ™Ab X x)))

-- Definition of main functions to use the informations of a group

âŒˆ_âŒ‹ : (X : AbGrp â„“) â†’ Type â„“
âŒˆ_âŒ‹ = fst

âˆ™â‚ : (X : AbGrp â„“) â†’ âŒˆ X âŒ‹
âˆ™â‚ = fst âˆ˜ snd

âŸ¨âŸ¨_âŸ©âŸ© : (X : AbGrp â„“) â†’ Pointed â„“
âŸ¨âŸ¨ X âŸ©âŸ© = âŒˆ X âŒ‹ , âˆ™â‚ X

âˆ¥â‰¡âˆ¥â‚ : (X : AbGrp â„“) â†’ connectedPath âŒˆ X âŒ‹
âˆ¥â‰¡âˆ¥â‚ = fst âˆ˜ snd âˆ˜ snd

groupAb : (X : AbGrp â„“) â†’ isGroupoid âŒˆ X âŒ‹
groupAb = fst âˆ˜ snd âˆ˜ snd âˆ˜ snd

â‰¡âˆ™â‚ : (X : AbGrp â„“) â†’ (x : âŒˆ X âŒ‹) â†’ PathP (Î» i â†’ Î£[ X âˆˆ Type â„“ ] X) (âŒˆ X âŒ‹ , âˆ™â‚ X) (âŒˆ X âŒ‹ , x)
â‰¡âˆ™â‚ = fst âˆ˜ snd âˆ˜ snd âˆ˜ snd âˆ˜ snd

â‰¡âˆ™â‚r : (X : AbGrp â„“) â†’ â‰¡âˆ™â‚ X (âˆ™â‚ X) â‰¡ refl
â‰¡âˆ™â‚r = snd âˆ˜ snd âˆ˜ snd âˆ˜ snd âˆ˜ snd

-- This is a lemma to show that the condition we use can be translated in being connected in the
-- deinition of cubical

connAb : (X : AbGrp â„“) â†’ isConnected 2 âŒˆ X âŒ‹
connAb X = subst isContr truncX ( âˆ£ âˆ™â‚ X âˆ£â‚‚ , sym âˆ˜ lemma)
  where
  truncX : âˆ¥ âŒˆ X âŒ‹ âˆ¥â‚‚ â‰¡ hLevelTrunc 2 âŒˆ X âŒ‹
  truncX = propTruncâ‰¡Trunc2 { A = âŒˆ X âŒ‹}
  lemma : (x : âˆ¥ âŒˆ X âŒ‹ âˆ¥â‚‚) â†’ x â‰¡ âˆ£ âˆ™â‚ X âˆ£â‚‚
  lemma = setelim (Î» x â†’ isPropâ†’isSet (isSetSetTrunc x âˆ£ âˆ™â‚ X âˆ£â‚‚))
    (Î» x â†’ proprec (isSetSetTrunc âˆ£ x âˆ£â‚‚ âˆ£ âˆ™â‚ X âˆ£â‚‚) (cong âˆ£_âˆ£â‚‚) (âˆ¥â‰¡âˆ¥â‚ X x (âˆ™â‚ X)))

-- This function allows to rotate a group and is quite useful

âˆ™â†’âˆ™â‚ : (X : AbGrp â„“) â†’ âŒˆ X âŒ‹ â†’ AbGrp â„“
âˆ™â†’âˆ™â‚ X x = âŒˆ X âŒ‹ , x , âˆ¥â‰¡âˆ¥â‚ X , groupAb X , (Î» xâ‚ â†’ â‰¡âˆ™â‚ X x â»Â¹ âˆ™ â‰¡âˆ™â‚ X xâ‚) , lCancel (â‰¡âˆ™â‚ X x)

-- Essential lemma for proofs of unicity : the set of dependant functions f : (x : A) -> B x
-- for A connected and B a family of n-types is a n-1-type if we fix one of the values of the f

is-1Typeâˆ™ : âˆ€ {â„“} {â„“'} â†’ (n : â„•) â†’ (A : Pointed â„“) â†’ (connectedPath âŸ¨ A âŸ©) â†’
  (B : (x : âŸ¨ A âŸ©) â†’ Type â„“') â†’ ((x : âŸ¨ A âŸ©) â†’ isOfHLevel (suc n) (B x) ) â†’ (b : B (pt A)) â†’
  isOfHLevel n (Î£[ f âˆˆ ((x : âŸ¨ A âŸ©) â†’ B(x)) ] (f (pt A) â‰¡ b))
is-1Typeâˆ™ zero A isconA B isPropB b = ((Î» x â†’ proprec (isPropB x) (Î» p â†’ subst B p b)
  (isconA (pt A) x)) , isPropB (pt A) _ _) , Î» f â†’ Î£â‰¡Prop (Î» f â†’ isPropâ†’isSet (isPropB (pt A))
  (f (pt A)) b) (funExt (Î» x â†’ isPropB x _ _))
is-1Typeâˆ™ (suc n) A isconA B isnlevelB b = isOfHLevelPath'â» n
  (Î» s1 s2 â†’ subst (Î» X â†’ isOfHLevel n X)
  ((cong (Î» a â†’ Î£-syntax ((x : âŸ¨ A âŸ©) â†’ fst s1 x â‰¡ fst s2 x) a) (funExt (Î» f â†’ ua (
  (f (pt A) â‰¡ snd s1 âˆ™ (sym (snd s2))) â‰ƒâŸ¨ isoToEquiv symIso âŸ©
  (snd s1 âˆ™ (sym (snd s2)) â‰¡ f (pt A)) â‰ƒâŸ¨ congEquiv (compPathrEquiv (snd s2)) âŸ©
  ((snd s1 âˆ™ sym (snd s2)) âˆ™ snd s2 â‰¡ f (pt A) âˆ™ snd s2)
    â‰ƒâŸ¨ compPathlEquiv ((rUnit (snd s1) âˆ™ cong (snd s1 âˆ™_) (sym (lCancel (snd s2))) âˆ™
    assoc (snd s1) (sym (snd s2)) (snd s2))) âŸ©
  snd s1 â‰¡ f (pt A) âˆ™ snd s2 â‰ƒâŸ¨ congEquiv (compPathlEquiv (sym (f (pt A)))) âŸ©
  (sym (f (pt A)) âˆ™ snd s1 â‰¡ sym (f (pt A)) âˆ™ f (pt A) âˆ™ snd s2)
    â‰ƒâŸ¨ compPathrEquiv (assoc (sym (f (pt A))) (f (pt A)) (snd s2) âˆ™
      cong (_âˆ™ snd s2) (lCancel (f (pt A))) âˆ™ sym (lUnit (snd s2))) âŸ©
  (sym (f (pt A)) âˆ™ snd s1 â‰¡ snd s2)
    â‰ƒâŸ¨ compPathlEquiv (sym (rUnit (sym (f (pt A)) âˆ™ snd s1) âˆ™
      sym (assoc (sym (f (pt A))) (snd s1) (refl)) âˆ™
      sym (substInPaths (idfun _) (Î» _ â†’ b) ((f (pt A))) ((snd s1))))) âŸ©
  (subst (Î» p â†’ p â‰¡ b) (f (pt A)) (snd s1) â‰¡ snd s2)
    â‰ƒâŸ¨ invEquiv (PathPâ‰ƒPath (Î» i â†’ f (pt A) i â‰¡ b) (snd s1) (snd s2)) âŸ©
  (PathP (Î» i â†’ (f (pt A) i) â‰¡ b) (snd s1) (snd s2)) â‰ƒâŸ¨ congPathEquiv (Î» i â†’ idEquiv _) âŸ©
  (PathP (Î» i â†’ funExt f i (pt A) â‰¡ b) (snd s1) (snd s2))
    â‰ƒâŸ¨ pathToEquiv (cong (Î» a â†’ PathP (Î» i â†’ a i (pt A) â‰¡ b) (snd s1) (snd s2))
      (sym (uaÎ² funExtEquiv f))) âŸ©
  (PathP (Î» i â†’ transport (funExtPath) f i (pt A) â‰¡ b) (snd s1) (snd s2) â– 
  )))) âˆ™ Î£-cong-fst funExtPath) âˆ™ Î£Pathâ‰¡PathÎ£)
  (is-1Typeâˆ™ n A isconA (Î» a â†’ fst s1 a â‰¡ fst s2 a)
  (Î» a â†’ isOfHLevelPath' (suc n) (isnlevelB a) (fst s1 a) (fst s2 a)) (snd s1 âˆ™ sym (snd s2))))

-- The first consequence is that being a group is just being a proposition on Pointed â„“

isPropHomo : (X : Type â„“) â†’ (x : X) â†’ (connectedPath X) â†’ (isGroupoid X) â†’ isProp (homoâˆ™Ab X x)
isPropHomo X x iscon isgrp = is-1Typeâˆ™ 1 (X , x) iscon
  (Î» xâ‚ â†’ (X , x) â‰¡ (X , xâ‚)) (Î» xâ‚ â†’ isOfHLevelRespectEquiv 2 (lemma (X â†’ X) (Î» f â†’ f x â‰¡ xâ‚)
  (isEquiv) âˆ™â‚‘ pointedSIP (X , x) (X , xâ‚)) (isSetÎ£SndProp
  (is-1Typeâˆ™ 2 (X , x) iscon (Î» xâ‚‚ â†’ X) (Î» _ â†’ isgrp) xâ‚) (Î» _ â†’ isPropIsEquiv _))) refl
  where
  lemma : âˆ€ {â„“ â„“' â„“''} â†’ (X : Type â„“) (Y : (x : X) â†’ Type â„“') (Z : (x : X) â†’ Type â„“'') â†’
    (Î£[ xy âˆˆ (Î£[ x âˆˆ X ] Y x)] Z (fst xy)) â‰ƒ (Î£[ xz âˆˆ (Î£[ x âˆˆ X ] Z x)] Y (fst xz))
  lemma X Y Z = isoToEquiv (record {
    fun = Î» x â†’ (fst (fst x) , snd x) , snd (fst x) ;
    inv = Î» x â†’ (fst (fst x) , snd x) , snd (fst x) ;
    leftInv = Î» x â†’ refl ;
    rightInv = Î» x â†’ refl})

Abâˆ™Prop : (X : Type â„“) â†’ (x : X) â†’ isProp ((connectedPath X) Ã— (isGroupoid X) Ã— (homoâˆ™Ab X x))
Abâˆ™Prop X x = isPropÎ£ (isPropÎ  (Î» x â†’ isPropÎ (Î» y â†’ isPropPropTrunc)))
  (Î» p â†’ isPropÎ£ isPropIsGroupoid (Î» q â†’ (isPropHomo X x p q)))

-- We now define the notion of morhism in ğ”¸ğ•“

AbMorph : (X : AbGrp â„“) â†’ (Y : AbGrp â„“) â†’ Type â„“
AbMorph X Y = âŸ¨âŸ¨ X âŸ©âŸ© â†’âˆ™ âŸ¨âŸ¨ Y âŸ©âŸ©

_â†’â‚_ = AbMorph

idâ‚ : (X : AbGrp â„“) â†’ X â†’â‚ X
idâ‚ X = idfun âŒˆ X âŒ‹ , refl

module _ (X : AbGrp â„“) (Y : AbGrp â„“) where

  0â‚ : X â†’â‚ Y
  0â‚ = (Î» _ â†’ âˆ™â‚ Y) , refl

  isSetâ†’â‚ : isSet (X â†’â‚ Y)
  isSetâ†’â‚ = is-1Typeâˆ™ 2 âŸ¨âŸ¨ X âŸ©âŸ© (âˆ¥â‰¡âˆ¥â‚ X) (Î» _ â†’ âŒˆ Y âŒ‹) (Î» _ â†’ groupAb Y) (âˆ™â‚ Y)

  â†’Aâ‰¡ : {f : X â†’â‚ Y} {g : X â†’â‚ Y} â†’ fst f â‰¡ fst g  â†’ f â‰¡ g
  â†’Aâ‰¡ p = â†’âˆ™Homogeneousâ‰¡ (â‰¡âˆ™â‚ Y) p

-- Basic properties about composition of morphisms

âˆ˜â‚ : (X Y Z : AbGrp â„“) â†’ (X â†’â‚ Y) â†’ (Y â†’â‚ Z) â†’ (X â†’â‚ Z)
âˆ˜â‚ X Y Z f g = g .fst âˆ˜ f .fst , (cong (g .fst) (f .snd) âˆ™ (g .snd))

lUnitMorph : (X Y : AbGrp â„“) â†’ (f : X â†’â‚ Y) â†’ âˆ˜â‚ X X Y (idâ‚ X) f â‰¡ f
lUnitMorph X Y f =  Î£PathP (âˆ˜-idË¡ (f .fst) , toPathP (transportRefl _ âˆ™ sym (lUnit (f .snd))))

rUnitMorph : (X Y : AbGrp â„“) â†’ (f : X â†’â‚ Y) â†’ âˆ˜â‚ X Y Y f (idâ‚ Y) â‰¡ f
rUnitMorph X Y f = Î£PathP (âˆ˜-idË¡ (fst f) , toPathP (transportRefl _ âˆ™ sym (rUnit (snd f))))

lAbsMorph : (X Y Z : AbGrp â„“) â†’ (f : Y â†’â‚ Z) â†’ âˆ˜â‚ X Y Z (0â‚ X Y) f â‰¡ 0â‚ X Z
lAbsMorph X Y Z f = â†’Aâ‰¡ X Z (funExt (Î» _ â†’ snd f))

rAbsMorph : (X Y Z : AbGrp â„“) â†’ (f : X â†’â‚ Y) â†’ âˆ˜â‚ X Y Z f (0â‚ Y Z) â‰¡ 0â‚ X Z
rAbsMorph X Y Z f = â†’Aâ‰¡ X Z (funExt (Î» _ â†’ refl))

assocMorph : (X Y Z A : AbGrp â„“) â†’ (f : X â†’â‚ Y) â†’ (g : Y â†’â‚ Z) â†’ (h : Z â†’â‚ A) â†’
  âˆ˜â‚ X Z A (âˆ˜â‚ X Y Z f g) h â‰¡ âˆ˜â‚ X Y A f (âˆ˜â‚ Y Z A g h)
assocMorph X Y Z A (f , fâˆ™) (g , gâˆ™) (h , hâˆ™) = sym (Î£PathP (refl , q))
  where
  q : (cong (h âˆ˜ g) fâˆ™) âˆ™ (cong h gâˆ™ âˆ™ hâˆ™) â‰¡ cong h (cong g fâˆ™ âˆ™ gâˆ™) âˆ™ hâˆ™
  q = ( (cong (h âˆ˜ g) fâˆ™) âˆ™ (cong h gâˆ™ âˆ™ hâˆ™)
      â‰¡âŸ¨ refl âŸ©
      (cong h (cong g fâˆ™)) âˆ™ (cong h gâˆ™ âˆ™ hâˆ™)
      â‰¡âŸ¨ assoc (cong h (cong g fâˆ™)) (cong h gâˆ™) hâˆ™ âŸ©
      (cong h (cong g fâˆ™) âˆ™ cong h gâˆ™) âˆ™ hâˆ™
      â‰¡âŸ¨ cong (Î» p â†’ p âˆ™ hâˆ™) ((cong-âˆ™ h (cong g fâˆ™) gâˆ™) â»Â¹) âŸ©
      (cong h (cong g fâˆ™ âˆ™ gâˆ™) âˆ™ hâˆ™) âˆ )

-- Definitions of equivalences in ğ”¸ğ•“ and a version of ua for it

EquivMorph : (X Y : AbGrp â„“) â†’ Type â„“
EquivMorph X Y = Î£[ e âˆˆ (âŒˆ X âŒ‹ â‰ƒ âŒˆ Y âŒ‹)] (e .fst (âˆ™â‚ X) â‰¡ âˆ™â‚ Y)

_â‰ƒâ‚_ = EquivMorph

idâ‰ƒâ‚ : (X : AbGrp â„“) â†’ (X â‰ƒâ‚ X)
idâ‰ƒâ‚ X = idEquiv âŒˆ X âŒ‹ , refl

module _ (X : AbGrp â„“) (Y : AbGrp â„“) where

  â‰ƒâ‚â†’â‚ : X â‰ƒâ‚ Y â†’ X â†’â‚ Y
  â‰ƒâ‚â†’â‚ e = (e .fst .fst , e .snd)

  â‰¡â‰ƒâ‚ : (e f : X â‰ƒâ‚ Y) â†’ (e â‰¡ f) â‰ƒ (â‰ƒâ‚â†’â‚ e â‰¡ â‰ƒâ‚â†’â‚ f)
  â‰¡â‰ƒâ‚ e f = invEquiv Î£Pathâ‰ƒPathÎ£ âˆ™â‚‘ Î£-cong-equiv-fst
    (invEquiv (Î£â‰¡PropEquiv (Î» x â†’ isPropIsEquiv x))) âˆ™â‚‘ Î£Pathâ‰ƒPathÎ£

  â‰¡fstsnd : (X â‰¡ Y) â†’ (âŸ¨âŸ¨ X âŸ©âŸ© â‰¡ âŸ¨âŸ¨ Y âŸ©âŸ©)
  â‰¡fstsnd e i = âŒˆ e i âŒ‹ , âˆ™â‚ (e i)
    
  expâ‰¡ : (âŸ¨âŸ¨ X âŸ©âŸ© â‰¡ âŸ¨âŸ¨ Y âŸ©âŸ©) â†’ (X â‰¡ Y)
  expâ‰¡ e i = âŸ¨ e i âŸ© , pt (e i) , isPropâ†’PathP
    (Î» i â†’ Abâˆ™Prop âŸ¨ e i âŸ© (pt (e i))) (X .snd .snd) (Y .snd .snd) i
  
  uaâ‚ : (X â‰ƒâ‚ Y) â†’ (X â‰¡ Y)
  uaâ‚ (e , p) = Î£PathP (ua (e) , Î£PathPProp (Î» y â†’ Abâˆ™Prop âŒˆ Y âŒ‹ y)
    (toPathP (uaÎ² e (âˆ™â‚ X) âˆ™ p)))

  pathToâ‰ƒâ‚ :(X â‰¡ Y) â†’ (X â‰ƒâ‚ Y)
  pathToâ‰ƒâ‚ e = pathToEquiv (cong âŒˆ_âŒ‹ e) , fromPathP (cong âˆ™â‚ e)

  uaâ‚Î² : (e : X â‰ƒâ‚ Y) â†’ pathToâ‰ƒâ‚ (uaâ‚ e) â‰¡ e
  uaâ‚Î² e = invEquiv (â‰¡â‰ƒâ‚ (pathToâ‰ƒâ‚ (uaâ‚ e)) e) .fst (â†’Aâ‰¡ X Y (funExt (Î» x â†’ uaÎ² (e .fst) x)))

record Isoâ‚ {â„“} (X : AbGrp â„“) (Y : AbGrp â„“) : Type (lsuc â„“) where
  constructor isoâ‚
  field
    funâ‚ : X â†’â‚ Y
    invâ‚ : Y â†’â‚ X
    rightInvâ‚ : âˆ˜â‚ Y X Y invâ‚ funâ‚ â‰¡ idâ‚ Y
    leftInvâ‚ : âˆ˜â‚ X Y X funâ‚ invâ‚ â‰¡ idâ‚ X

Isoâ‚â†’Iso : (X : AbGrp â„“) (Y : AbGrp â„“) â†’ Isoâ‚ X Y â†’ Iso âŒˆ X âŒ‹ âŒˆ Y âŒ‹
Isoâ‚â†’Iso X Y f .Iso.fun = f .Isoâ‚.funâ‚ .fst
Isoâ‚â†’Iso X Y f .Iso.inv = f .Isoâ‚.invâ‚ .fst
Isoâ‚â†’Iso X Y f .Iso.leftInv x = cong (Î» f â†’ fst f x) (f .Isoâ‚.leftInvâ‚)
Isoâ‚â†’Iso X Y f .Iso.rightInv x = cong (Î» f â†’ fst f x) (f .Isoâ‚.rightInvâ‚)

Isoâ‚â†’â‰ƒâ‚ : (X : AbGrp â„“) (Y : AbGrp â„“) â†’ Isoâ‚ X Y â†’ X â‰ƒâ‚ Y
Isoâ‚â†’â‰ƒâ‚ X Y f = isoToEquiv (Isoâ‚â†’Iso X Y f) , f .Isoâ‚.funâ‚ .snd

-- Description of the "group" operation +â‚ given by homogeneity in an abelian group
-- and proofs that (âŒˆXâŒ‹,+â‚) is indeed an abelian group except for being a set

module _ (X : AbGrp â„“) where

  +â‚ : âŒˆ X âŒ‹ â†’ âŒˆ X âŒ‹ â†’ âŒˆ X âŒ‹
  +â‚ x = transport (cong fst (â‰¡âˆ™â‚ X x))

  actAbIdf : +â‚ (âˆ™â‚ X) â‰¡ idfun âŒˆ X âŒ‹
  actAbIdf = funExt (Î» x â†’ ((Î» i â†’ transport (Î» j â†’ â‰¡âˆ™â‚r X i j .fst) x) âˆ™ transportRefl x))

  actAbIdr : âˆ€ x â†’ +â‚ x (âˆ™â‚ X) â‰¡ x
  actAbIdr x = fromPathP (PathPÎ£ (â‰¡âˆ™â‚ X x) .snd)

  actAbIdl : âˆ€ x â†’ +â‚ (âˆ™â‚ X) x â‰¡ x
  actAbIdl x = cong (_$ x) actAbIdf

  abHSpace : HSpace âŸ¨âŸ¨ X âŸ©âŸ©
  abHSpace = Iso.inv (HSpace-Î âˆ™-Iso âŸ¨âŸ¨ X âŸ©âŸ©) s
    where
    s : Î âˆ™ âŸ¨âŸ¨ X âŸ©âŸ© (Î» x â†’ âŸ¨âŸ¨ X âŸ©âŸ© â†’âˆ™ (âŒˆ X âŒ‹ , x)) (idfunâˆ™ âŸ¨âŸ¨ X âŸ©âŸ©)
    fst s x = (+â‚ x , actAbIdr x)
    snd s = â†’âˆ™Homogeneousâ‰¡ (X .snd .snd .snd .snd .fst) actAbIdf

  leftInvAbHSpace : LeftInvHSpace abHSpace
  LeftInvHSpace.Î¼-equiv leftInvAbHSpace x = snd (pathToEquiv (cong fst (â‰¡âˆ™â‚ X x)))

  +cfun : Type â„“
  +cfun = Î£[ g âˆˆ ((x : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ X x))] (g (âˆ™â‚ X) â‰¡ idâ‚ X)

  isProp+cfun : isProp +cfun
  isProp+cfun = is-1Typeâˆ™ 1 âŸ¨âŸ¨ X âŸ©âŸ© (âˆ¥â‰¡âˆ¥â‚ X)
    (Î» x â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ X x)) (Î» x â†’ isSetâ†’â‚ X (âˆ™â†’âˆ™â‚ X x)) (idâ‚ X)

  +cfl : +cfun
  +cfl .fst xâ‚€ .fst xâ‚ = +â‚ xâ‚€ xâ‚
  +cfl .fst xâ‚€ .snd = actAbIdr xâ‚€
  +cfl .snd = â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ X (âˆ™â‚ X)) (funExt actAbIdl)
  
  +cfr : +cfun
  +cfr .fst xâ‚€ .fst xâ‚ = +â‚ xâ‚ xâ‚€
  +cfr .fst xâ‚€ .snd = actAbIdl xâ‚€
  +cfr .snd = â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ X (âˆ™â‚ X)) (funExt actAbIdr)

  +â‚com : âˆ€ xâ‚€ xâ‚ â†’ +â‚ xâ‚€ xâ‚ â‰¡ +â‚ xâ‚ xâ‚€
  +â‚com xâ‚€ xâ‚ = cong (Î» a â†’ a .fst xâ‚€ .fst xâ‚) (isProp+cfun +cfl +cfr)

  +afun : Type â„“
  +afun = Î£[ g âˆˆ ((xâ‚€ : âŒˆ X âŒ‹) â†’ (xâ‚ : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ X (+â‚ xâ‚€ xâ‚)))] (g (âˆ™â‚ X) â‰¡ fun)
    where
    fun : (xâ‚ : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ X (+â‚ (âˆ™â‚ X) xâ‚))
    fun xâ‚ = (Î» xâ‚‚ â†’ +â‚ (+â‚ (âˆ™â‚ X) xâ‚) xâ‚‚) , actAbIdr (+â‚ (âˆ™â‚ X) xâ‚)

  isProp+afun : isProp +afun
  isProp+afun = is-1Typeâˆ™ 1 âŸ¨âŸ¨ X âŸ©âŸ© (âˆ¥â‰¡âˆ¥â‚ X)
    (Î» xâ‚€ â†’ (xâ‚ : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ X (+â‚ xâ‚€ xâ‚)))
    (Î» xâ‚€ â†’ isSetÎ  (Î» xâ‚ â†’ isSetâ†’â‚ X (âˆ™â†’âˆ™â‚ X (+â‚ xâ‚€ xâ‚))))
    (Î» xâ‚ â†’ (Î» xâ‚‚ â†’ +â‚ (+â‚ (âˆ™â‚ X) xâ‚) xâ‚‚) , actAbIdr (+â‚ (âˆ™â‚ X) xâ‚))

  +afl : +afun
  +afl .fst xâ‚€ xâ‚ .fst xâ‚‚ = +â‚ (+â‚ xâ‚€ xâ‚) xâ‚‚
  +afl .fst xâ‚€ xâ‚ .snd = actAbIdr (+â‚ xâ‚€ xâ‚)
  +afl .snd = funExt (Î» xâ‚ â†’ â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ X (+â‚ (âˆ™â‚ X) xâ‚))
    (funExt (Î» xâ‚‚ â†’ refl)))

  +afr : +afun
  +afr .fst xâ‚€ xâ‚ .fst xâ‚‚ = +â‚ xâ‚€ (+â‚ xâ‚ xâ‚‚)
  +afr .fst xâ‚€ xâ‚ .snd = cong (Î» a â†’ +â‚ xâ‚€ a) (actAbIdr xâ‚)
  +afr .snd = funExt (Î» xâ‚ â†’ â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ X (+â‚ (âˆ™â‚ X) xâ‚))
    (funExt (Î» xâ‚‚ â†’ actAbIdl (+â‚ xâ‚ xâ‚‚) âˆ™ cong (Î» a â†’ +â‚ a xâ‚‚) (sym (actAbIdl xâ‚)))))

  +â‚assoc : âˆ€ xâ‚€ xâ‚ xâ‚‚ â†’ +â‚ (+â‚ xâ‚€ xâ‚) xâ‚‚ â‰¡ +â‚ xâ‚€ (+â‚ xâ‚ xâ‚‚)
  +â‚assoc xâ‚€ xâ‚ xâ‚‚ = cong (Î» a â†’ a .fst xâ‚€ xâ‚ .fst xâ‚‚) (isProp+afun +afl +afr)

  _â»Â¹áµƒ : âŒˆ X âŒ‹ â†’ âŒˆ X âŒ‹
  x â»Â¹áµƒ = transport (cong fst (sym (â‰¡âˆ™â‚ X x))) (âˆ™â‚ X)

  lCancelâ‚ : âˆ€ x â†’ +â‚ x (x â»Â¹áµƒ) â‰¡ âˆ™â‚ X
  lCancelâ‚ x = sym (transportComposite (cong fst (sym (â‰¡âˆ™â‚ X x))) (cong fst (â‰¡âˆ™â‚ X x)) (âˆ™â‚ X)) âˆ™
    cong (Î» a â†’ transport a (âˆ™â‚ X)) (sym (cong-âˆ™ fst (sym (â‰¡âˆ™â‚ X x)) (â‰¡âˆ™â‚ X x))) âˆ™
    cong (Î» a â†’ transport (cong fst a) (âˆ™â‚ X)) (lCancel (â‰¡âˆ™â‚ X x)) âˆ™ transportRefl (âˆ™â‚ X)

  rCancelâ‚ : âˆ€ x â†’ +â‚ (x â»Â¹áµƒ) x â‰¡ âˆ™â‚ X
  rCancelâ‚ x = +â‚com (x â»Â¹áµƒ) x âˆ™ lCancelâ‚ x

-- A morphism in ğ”¸ğ•“[X,Y] commutes with +â‚

module _ (X : AbGrp â„“) (Y : AbGrp â„“) ((f , fâˆ™) : X â†’â‚ Y) where

  â†’+ : (x : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ Y (f x))
  â†’+ x = (Î» xâ‚ â†’ +â‚ Y (f x) (f xâ‚)) , cong (Î» a â†’ +â‚ Y (f x) a) fâˆ™ âˆ™ actAbIdr Y (f x)

  +â†’ : (x : âŒˆ X âŒ‹)  â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ Y (f x))
  +â†’ x = (Î» xâ‚ â†’ f (+â‚ X x xâ‚)) , cong f (actAbIdr X x)

  bifunâˆ™ : Type â„“
  bifunâˆ™ = Î£[ g âˆˆ ((x : âŒˆ X âŒ‹) â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ Y (f x)))] (g (âˆ™â‚ X) â‰¡ (f , refl))

  isPropbifunâˆ™ : isProp bifunâˆ™
  isPropbifunâˆ™ = is-1Typeâˆ™ 1 âŸ¨âŸ¨ X âŸ©âŸ© (âˆ¥â‰¡âˆ¥â‚ X)
    (Î» x â†’ X â†’â‚ (âˆ™â†’âˆ™â‚ Y (f x))) (Î» x â†’ isSetâ†’â‚ X (âˆ™â†’âˆ™â‚ Y (f x))) (f , refl)

  â†’+âˆˆbifunâˆ™ : bifunâˆ™
  â†’+âˆˆbifunâˆ™ = â†’+ , â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ Y (f (âˆ™â‚ X)))
    (funExt (Î» x â†’ cong (Î» a â†’ +â‚ Y a (f x)) (fâˆ™) âˆ™ actAbIdl Y (f x)))

  +â†’âˆˆbifunâˆ™ : bifunâˆ™
  +â†’âˆˆbifunâˆ™ = +â†’ , â†’Aâ‰¡ X (âˆ™â†’âˆ™â‚ Y (f (âˆ™â‚ X)))
    (funExt (Î» x â†’ cong f (actAbIdl X x)))

  +â†’â‰¡â†’+ : â†’+ â‰¡ +â†’
  +â†’â‰¡â†’+ = cong fst (isPropbifunâˆ™ â†’+âˆˆbifunâˆ™ +â†’âˆˆbifunâˆ™)

  â†’â‚+ : (xâ‚€ xâ‚ : âŒˆ X âŒ‹) â†’ +â‚ Y (f xâ‚€) (f xâ‚) â‰¡ f (+â‚ X xâ‚€ xâ‚)
  â†’â‚+ xâ‚€ xâ‚ = cong (Î» a â†’ a xâ‚€ .fst xâ‚) +â†’â‰¡â†’+

-- {0} is both initial and terminal

nulâ‚ : AbGrp â„“
nulâ‚ = Unit* , tt* , (Î» x â†’ Î» y â†’ âˆ£ isOfHLevelLift 1 isPropUnit x y âˆ£â‚) ,
  isOfHLevelLift 3 (isOfHLevelUnit 3) , (Î» x i â†’ Unit* , isOfHLevelLift 1 isPropUnit tt* x i),
  Î» i j â†’ Lift Unit , isOfHLevelLift 2 isSetUnit tt* tt*
    (isOfHLevelLift 1 isPropUnit tt* tt*) refl i j

initNulâ‚ : (X : AbGrp â„“) â†’ isContr (nulâ‚ â†’â‚ X)
initNulâ‚ X = 0â‚ nulâ‚ X , Î» f â†’ â†’Aâ‰¡ nulâ‚ X
  (funExt (Î» x â†’ sym (snd f) âˆ™ cong (fst f) (isOfHLevelLift 1 isPropUnit tt* x)))

finNulâ‚ : (X : AbGrp â„“) â†’ isContr (X â†’â‚ nulâ‚)
finNulâ‚ X = 0â‚ X nulâ‚ , Î» f â†’ â†’Aâ‰¡ X nulâ‚
  (funExt (Î» x â†’ isOfHLevelLift 1 isPropUnit tt* (fst f x)))
